\usepath[/home/adityam/Projects/presentations]

\enablemode[euler]
\environment env-fonts.tex

\startMPdefinitions
% Arrowhead Modifications for TAOCP. Copied from some webpage of Knuth. 
% These arrows are better in appearance than the default mp arrows.
ahangle := 65;
ahlength := 10 ;
vardef arrowhead expr p =
  save q, e, f, g; path q; pair e; pair f; pair g;
  e = point length p of p;
  q = gobble(p shifted -e cutafter makepath(pencircle scaled 2ahlength))
    cuttings;
  f = point 0 of (q rotated 0.5ahangle) shifted e;
  g = point length q of (reverse q rotated -0.5ahangle) shifted e;
  f .. {dir (angle direction length q of (q rotated 0.5ahangle) - 0.3ahangle)}e
    & e{dir (angle direction 0 of ((reverse q) rotated -0.5ahangle)+0.3ahangle)} .. g
enddef;

vardef arrowpath expr p =
  p
enddef;

def mfun_draw_arrow_path text t =
    if autoarrows :
        set_ahlength(t) ;
    fi
    draw arrowpath mfun_arrow_path t ;
    draw arrowhead mfun_arrow_path t ;
    endgroup ;
enddef ;

def mfun_draw_arrow_path_double text t =
    if autoarrows :
        set_ahlength(t) ;
    fi
    draw arrowpath (reverse arrowpath mfun_arrow_path) t ;
    draw arrowhead mfun_arrow_path t ;
    draw arrowhead reverse mfun_arrow_path t ;
    endgroup ;
enddef ;


  primarydef a -+ b =
    a -- (xpart b, ypart a) -- b
  enddef ;

  primarydef a +- b =
    a -- (xpart a, ypart b) -- b
  enddef ;

  input boxes;
  input mp-sketch;
  sketchypaths;
  sketch_amount := 1.3bp;
  pickup pencircle scaled 2bp;
  defaultdx := 10bp;
  defaultdy := 10bp;

\stopMPdefinitions


\starttext
\startMPpage[offset=1dk] %{{{ LTI System
  begingroup;
  sx := 1cm;
  sy := 1cm;

  boxit.LTI("LTI System");
  LTI.c = origin;

  boxes_fixsize(LTI);
  boxes_fixpos (LTI);

  drawunboxed(LTI);

  pair Input, Output;

  Input  := LTI.w - (sx,0);
  Output := LTI.e + (sx,0);

  for i = 1 upto 5 :
    sketch_amount := 1.3bp;
    draw bpath LTI cornered 10bp;
    
    sketch_amount := 0.6bp;
    drawarrow Input -- lft LTI.w;
    drawarrow LTI.e -- Output;
  endfor;

  label.lft("$u(t)$", Input);
  label.rt("$y(t)$", Output);

  currentpicture := currentpicture scaled 1.5;

  endgroup;
\stopMPpage % }}}

\startMPpage[offset=1dk] %{{{ LTI System
  begingroup;
  sketchypaths; 
  sx := 1cm;
  sy := 1cm;

  boxit.LTI("$G(s)$");
  LTI.c = origin;

  boxes_fixsize(LTI);
  boxes_fixpos (LTI);

  drawunboxed(LTI);

  pair Input, Output;

  Input  := LTI.w - (sx,0);
  Output := LTI.e + (sx,0);

  for i = 1 upto 5 :
    sketch_amount := 1.3bp;
    draw bpath LTI cornered 10bp;
    
    sketch_amount := 0.6bp;
    drawarrow Input -- lft LTI.w;
    drawarrow LTI.e -- Output;
  endfor;

  label.lft("$U(s)$", Input);
  label.rt("$Y(s)$", Output);

  currentpicture := currentpicture scaled 1.5;

  endgroup;
\stopMPpage % }}}

\startMPpage[offset=1dk] % {{{ Series 
  begingroup;
  sketchypaths;

  sx := 1cm;
  sy := 1cm;

  boxit.LTI_one("$G_1(s)$");
  boxit.LTI_two("$G_2(s)$");

  LTI_two.w - LTI_one.e = (2sx,0);

  boxes_fixsize(LTI_one,LTI_two);
  boxes_fixpos (LTI_one,LTI_two);

  drawunboxed(LTI_one,LTI_two);

  pair Input, Output;

  Input  := LTI_one.w - (sx,0);
  Output := LTI_two.e + (sx,0);

  for i = 1 upto 5 :
    sketch_amount := 1.3bp;
    draw bpath LTI_one cornered 10bp;
    draw bpath LTI_two cornered 10bp;
    
    sketch_amount := 0.6bp;
    drawarrow Input -- lft LTI_one.w;
    drawarrow LTI_one.e -- lft LTI_two.w;
    drawarrow LTI_two.e -- Output;
  endfor;

  label.lft("$U(s)$", Input);
  label.rt("$Y(s)$", Output);

  currentpicture := currentpicture scaled 1.5;

  endgroup;
\stopMPpage % }}}

\startMPpage[offset=1dk] % {{{ Parallel
  begingroup;
  sketchypaths;

  sx := 1cm;
  sy := 1cm;

  pair split_one;
  boxit.LTI_one("$G_1(s)$");
  boxit.LTI_two("$G_2(s)$");
  circleit.sum_one("\tfc $+$");

  split_one = origin;

  LTI_one.s - LTI_two.n = (0,1.5sy);
  xpart LTI_one.w - xpart split_one = 1.5sx;
  sum_one.w - 0.5[ LTI_one.se, LTI_two.ne ] = (1.5sx,0);

  boxes_fixsize(sum_one,LTI_one,LTI_two);
  boxes_fixpos (sum_one,LTI_one,LTI_two);

  drawunboxed(sum_one,LTI_one,LTI_two);

  pair Input, Output;

  Input  := split_one - (sx,0);
  Output := sum_one.e + (sx,0);

  for i = 1 upto 5 :
    sketch_amount := 1.3bp;
    draw bpath LTI_one cornered 10bp;
    draw bpath LTI_two cornered 10bp;
    draw bpath sum_one;
    
    sketch_amount := 0.6bp;
    draw Input -- split_one;
    drawarrow split_one +- lft LTI_one.w;
    drawarrow split_one +- lft LTI_two.w;
    drawarrow LTI_one.e -+ top sum_one.n ;
    drawarrow LTI_two.e -+ bot sum_one.s ; 
    drawarrow sum_one.e -- Output;
  endfor;


  label.urt("\tfx $+$", sum_one.n + (2bp,0) ); 
  label.lrt("\tfx $+$", sum_one.s + (2bp,0) ); 
  label.lft("$U(s)$", Input);
  label.rt("$Y(s)$", Output);

  currentpicture := currentpicture scaled 1.5;

  endgroup;
\stopMPpage % }}}

\startMPpage[offset=1dk] % {{{ Feedback
  begingroup;
  sketchypaths;

  sx := 1cm;
  sy := 1cm;

  pair split_one, split_two;
  boxit.LTI_one("$G(s)$");
  boxit.LTI_two("$H(s)$");
  circleit.sum_one("\tfc $+$");

  split_one = origin;

  LTI_one.w - sum_one.e = (1.5sx,0);
  LTI_one.s - LTI_two.n = (0, sy);

  boxes_fixsize(sum_one,LTI_one,LTI_two);
  boxes_fixpos (sum_one,LTI_one,LTI_two);

  drawunboxed(sum_one,LTI_one,LTI_two);

  pair Input, Output;

  Input  := split_one - (1.5sx,0);
  split_two := LTI_one.e + (sx,0);
  Output := split_two + (sx,0);

  for i = 1 upto 5 :
    sketch_amount := 1.3bp;
    draw bpath LTI_one cornered 10bp;
    draw bpath LTI_two cornered 10bp;
    draw bpath sum_one;
    
    sketch_amount := 0.6bp;
    drawarrow Input -- sum_one.w;
    drawarrow sum_one.e -- lft LTI_one.w;
    drawarrow LTI_one.e -- Output;
    drawarrow LTI_two.w -+ bot sum_one.s ;
    drawarrow split_two +- lft LTI_two.e;
  endfor;

  label.ulft("\tfx $+$", sum_one.w + (2bp,2bp)); 
  label.lrt("\tfx $-$", sum_one.s + (2bp,0) ); 
  label.lft("$U(s)$", Input);
  label.rt("$Y(s)$", Output);

  currentpicture := currentpicture scaled 1.5;

  endgroup;
\stopMPpage % }}}

\startMPpage[offset=1dk] % {{{ Full Feedback
  begingroup;
  sketchypaths;

  sx := 1cm;
  sy := 1cm;

  pair split_one, split_two;
  boxit.LTI_one("$G(s)$");
  boxit.LTI_two("$G_c(s)$");
  circleit.sum_one("\tfc $+$");
  circleit.sum_two("\tfc $+$");
  circleit.sum_three("\tfc $+$");

  sum_one.c = origin;

  LTI_one.w - sum_one.e = (sx,0);
  sum_two.w - LTI_one.e = (sx,0);
  LTI_two.w - sum_two.e = (sx,0);
  sum_three.w - LTI_two.e = (sx,0);

  boxes_fixsize(sum_one,LTI_one,LTI_two,sum_two, sum_three);
  boxes_fixpos (sum_one,LTI_one,LTI_two,sum_two, sum_three);

  drawunboxed(sum_one,LTI_one,LTI_two, sum_two, sum_three);

  pair Input, Output;
  pair D_one, D_two;

  Input  := sum_one.w - (sx,0);
  split_two := sum_three.e + (sx,0);
  Output := split_two + (sx,0);

  split_one := LTI_two.s - (0, sy);

  D_one := sum_two.n + (0, sy);
  D_two := sum_three.n + (0, sy);

  for i = 1 upto 5 :
    sketch_amount := 1.3bp;
    draw bpath LTI_one cornered 10bp;
    draw bpath LTI_two cornered 10bp;
    draw bpath sum_one;
    draw bpath sum_two;
    draw bpath sum_three;
    
    sketch_amount := 0.6bp;
    drawarrow Input -- sum_one.w;
    drawarrow sum_one.e -- lft LTI_one.w;
    drawarrow LTI_one.e -- lft sum_two.w;
    drawarrow sum_two.e -- lft LTI_two.w;
    drawarrow LTI_two.e -- lft sum_three.w;
    drawarrow sum_three.e -- Output;

    drawarrow split_two +- split_one -+ sum_one.s;
    drawarrow D_one -- top sum_two.n ;
    drawarrow D_two -- top sum_three.n ;
  endfor;

  label.ulft("\tfx $+$", sum_one.w + (2bp,2bp)); 
  label.lrt("\tfx $-$", sum_one.s + (2bp,0) ); 

  label.ulft("\tfx $+$", sum_two.w + (2bp,2bp)); 
  label.urt("\tfx $+$", sum_two.n + (2bp,0) ); 

  label.ulft("\tfx $+$", sum_three.w + (2bp,2bp)); 
  label.urt("\tfx $+$", sum_three.n + (2bp,0) ); 

  label.lft("$R(s)$", Input);
  label.rt("$Y(s)$", Output);

  label.top("$D_1(s)$", D_one);
  label.top("$D_2(s)$", D_two);

  currentpicture := currentpicture scaled 1.5;

  endgroup;
\stopMPpage % }}}

\stoptext

% vim: foldmethod=marker
